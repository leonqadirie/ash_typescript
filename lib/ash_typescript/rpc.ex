# SPDX-FileCopyrightText: 2025 ash_typescript contributors <https://github.com/ash-project/ash_typescript/graphs/contributors>
#
# SPDX-License-Identifier: MIT

defmodule AshTypescript.Rpc do
  @moduledoc false

  defmodule RpcAction do
    @moduledoc """
    Struct representing an RPC action configuration.

    Defines the mapping between a named RPC endpoint and an Ash action.
    """
    defstruct [
      :name,
      :action,
      :namespace,
      :description,
      :deprecated,
      :see,
      :read_action,
      :show_metadata,
      :metadata_field_names,
      :get?,
      :get_by,
      :not_found_error?,
      :identities,
      :enable_filter?,
      :enable_sort?,
      :allowed_loads,
      :denied_loads,
      __spark_metadata__: nil
    ]
  end

  defmodule Resource do
    @moduledoc """
    Struct representing a resource's RPC configuration.

    Contains the resource module and lists of configured RPC actions
    and typed queries for that resource.
    """
    defstruct [:resource, :namespace, rpc_actions: [], typed_queries: [], __spark_metadata__: nil]
  end

  defmodule TypedQuery do
    @moduledoc """
    Struct representing a typed query configuration.

    Defines a pre-configured query with specific fields and TypeScript types,
    allowing for type-safe, reusable query patterns in the generated RPC client.
    """
    defstruct [
      :name,
      :description,
      :ts_result_type_name,
      :ts_fields_const_name,
      :resource,
      :action,
      :fields,
      __spark_metadata__: nil
    ]
  end

  @typed_query %Spark.Dsl.Entity{
    name: :typed_query,
    target: TypedQuery,
    schema: [
      action: [
        type: :atom,
        doc: "The read action on the resource to query"
      ],
      name: [
        type: :atom,
        doc: "The name of the RPC-action"
      ],
      description: [
        type: :string,
        doc: "Description for the JSDoc comment of this typed query.",
        required: false
      ],
      ts_result_type_name: [
        type: :string,
        doc: "The name of the TypeScript type for the query result"
      ],
      ts_fields_const_name: [
        type: :string,
        doc:
          "The name of the constant for the fields, that can be reused by the client to re-run the query"
      ],
      fields: [
        type: {:list, :any},
        doc: "The fields to query"
      ]
    ],
    args: [:name, :action]
  }

  @rpc_action %Spark.Dsl.Entity{
    name: :rpc_action,
    target: RpcAction,
    describe: """
    Define an RPC action that exposes a resource action to TypeScript clients.

    Metadata fields: Action metadata can be exposed via `show_metadata` option.
    Set to `nil` (default) to expose all metadata fields, `false` or `[]` to disable,
    or provide a list of atoms to expose specific fields.

    Metadata field naming: Use `metadata_field_names` to map invalid metadata field names
    (e.g., `field_1`, `is_valid?`) to valid TypeScript identifiers.
    Example: `metadata_field_names [field_1: "field1", is_valid?: "isValid"]`

    Get options:
    - `get?` - When true, retrieves a single resource by primary key. Requires primary key
      in the RPC call and returns a single result or null.
    - `get_by` - Retrieves a single resource by the specified fields. The fields must be
      valid resource attributes. Returns a single result or null.
    """,
    schema: [
      name: [
        type: :atom,
        doc: "The name of the RPC-action"
      ],
      action: [
        type: :atom,
        doc: "The resource action to expose"
      ],
      namespace: [
        type: :string,
        doc:
          "Namespace for organizing this action into a separate file (becomes the filename). Overrides resource/domain namespace.",
        required: false
      ],
      description: [
        type: :string,
        doc:
          "Custom description for the JSDoc comment. Always shown when set, overrides the action's internal description.",
        required: false
      ],
      deprecated: [
        type: {:or, [:boolean, :string]},
        doc:
          "Mark this action as deprecated. Set to true for a default message, or provide a custom deprecation notice (e.g., \"Use listTodosV2 instead\").",
        required: false
      ],
      see: [
        type: {:list, :atom},
        doc:
          "List of related RPC action names to reference in JSDoc @see tags (e.g., `see: [:list_todos, :create_todo]`).",
        default: []
      ],
      read_action: [
        type: :atom,
        doc: "The read action to use for update and destroy operations when finding records",
        required: false
      ],
      show_metadata: [
        type: {:or, [nil, :boolean, {:list, :atom}]},
        doc: "Which metadata fields to expose (nil=all, false/[]=none, list=specific fields)",
        default: nil
      ],
      metadata_field_names: [
        type: {:list, {:tuple, [:atom, :string]}},
        doc: "Map metadata field names to valid TypeScript identifiers (string values)",
        default: []
      ],
      get?: [
        type: :boolean,
        doc:
          "When true, retrieves a single resource by primary key. Returns single result or null.",
        default: false
      ],
      get_by: [
        type: {:list, :atom},
        doc:
          "Retrieves a single resource by the specified fields (must be resource attributes). Returns single result or null.",
        default: []
      ],
      not_found_error?: [
        type: {:in, [true, false, nil]},
        doc:
          "When true (default from global config), returns an error if no record is found. When false, returns null. Only applies to get actions (get?, get_by, or action.get?). If not specified, uses the global config `config :ash_typescript, not_found_error?: true`.",
        default: nil
      ],
      identities: [
        type: {:list, :atom},
        doc:
          "List of identities that can be used to look up records for update/destroy actions. Use `:_primary_key` for the primary key, or identity names like `:email`. Defaults to `[:_primary_key]`. Use `[]` for actor-scoped actions that don't need a lookup key.",
        default: [:_primary_key]
      ],
      enable_filter?: [
        type: :boolean,
        doc:
          "When false, disables filter support for this read action. The filter key will not be included in the generated TypeScript config, the action's filter type won't be generated, and any filter sent by the client will be dropped. Defaults to true.",
        default: true
      ],
      enable_sort?: [
        type: :boolean,
        doc:
          "When false, disables sort support for this read action. The sort key will not be included in the generated TypeScript config, and any sort sent by the client will be dropped. Defaults to true.",
        default: true
      ],
      allowed_loads: [
        type: {:list, :any},
        doc:
          "Restricts loadable fields to only those specified. Accepts atoms for simple fields or keyword lists for nested fields (e.g., `[:user, comments: [:author]]`). Mutually exclusive with `denied_loads`."
      ],
      denied_loads: [
        type: {:list, :any},
        doc:
          "Denies loading of the specified fields. Accepts atoms for simple fields or keyword lists for nested fields (e.g., `[:user, comments: [:author]]`). Mutually exclusive with `allowed_loads`."
      ]
    ],
    args: [:name, :action]
  }

  @resource %Spark.Dsl.Entity{
    name: :resource,
    target: Resource,
    describe: "Define available RPC-actions for a resource",
    schema: [
      resource: [
        type: {:spark, Ash.Resource},
        doc: "The resource being configured"
      ],
      namespace: [
        type: :string,
        doc: "Default namespace (filename) for all actions in this resource.",
        required: false
      ]
    ],
    args: [:resource],
    entities: [
      rpc_actions: [@rpc_action],
      typed_queries: [@typed_query]
    ]
  }

  @rpc %Spark.Dsl.Section{
    name: :typescript_rpc,
    describe: """
    Define available RPC-actions for resources in this domain.

    The error handler will be called with (error, context) and should return a modified error map.
    If a module is provided, it must export a handle_error/2 function.

    Default error handler: {AshTypescript.Rpc.DefaultErrorHandler, :handle_error, []}

    Example:
    ```elixir
    error_handler {MyApp.CustomErrorHandler, :handle_error, []}
    # or
    error_handler MyApp.CustomErrorHandler

    show_raised_errors?:
    Set to true in development to see full error details.
    Keep false in production for security.
    ```
    """,
    schema: [
      error_handler: [
        type: {:or, [:mfa, :module]},
        doc: "An MFA or module that implements error handling for RPC operations.",
        default: {AshTypescript.Rpc.DefaultErrorHandler, :handle_error, []}
      ],
      show_raised_errors?: [
        type: :boolean,
        default: false,
        doc: "Whether to show detailed information for raised exceptions."
      ],
      namespace: [
        type: :string,
        doc: "Default namespace (filename) for all resources in this domain.",
        required: false
      ]
    ],
    entities: [
      @resource
    ]
  }

  use Spark.Dsl.Extension, sections: [@rpc]

  alias AshTypescript.Rpc.{ErrorBuilder, Errors, Pipeline}

  def codegen(args) do
    Mix.Task.reenable("ash_typescript.codegen")
    Mix.Task.run("ash_typescript.codegen", args)
  end

  @doc """
  Determines if tenant parameters are required in RPC requests.

  This checks the application configuration for :require_tenant_parameters.
  If true, tenant parameters are required for multitenant resources.
  If false (default), tenant will be extracted from the connection using
  Ash.PlugHelpers.get_tenant/1 (an explicitly provided tenant parameter still
  takes precedence).
  """
  def require_tenant_parameters? do
    Application.get_env(:ash_typescript, :require_tenant_parameters, false)
  end

  @doc """
  Gets the input field formatter configuration for parsing input parameters from the client.

  Delegates to `AshTypescript.input_field_formatter/0`.
  """
  defdelegate input_field_formatter, to: AshTypescript

  @doc """
  Gets the output field formatter configuration for TypeScript generation and responses to the client.

  Delegates to `AshTypescript.output_field_formatter/0`.
  """
  defdelegate output_field_formatter, to: AshTypescript

  @doc """
  Determines if Zod schema generation is enabled.

  When true, generates Zod schemas alongside TypeScript types for runtime validation.
  Defaults to false (opt-in feature).
  """
  def generate_zod_schemas? do
    Application.get_env(:ash_typescript, :generate_zod_schemas, false)
  end

  @doc """
  Gets the Zod schema overrides for custom types from application configuration.

  Use this for hand-rolled custom types whose generated schema you want to
  control — it is the Zod counterpart to `type_mapping_overrides`. Types you own
  are usually better expressed as an `Ash.Type.NewType` with constraints, which
  needs no override at all: a NewType carries its constraints through the normal
  dispatch and never reaches the custom-type fallback.

  ## Configuration

      config :ash_typescript,
        zod_mapping_overrides: [
          {AshObjectIds.Type, "z.string()"}
        ]

  ## Returns
  A keyword list of `{type_module, zod_schema_string}` tuples, or an empty list
  if not configured.
  """
  def zod_mapping_overrides do
    Application.get_env(:ash_typescript, :zod_mapping_overrides, [])
  end

  @doc """
  Gets the Valibot schema overrides for custom types from application configuration.

  The Valibot counterpart to `zod_mapping_overrides/0`.

  ## Configuration

      config :ash_typescript,
        valibot_mapping_overrides: [
          {AshObjectIds.Type, "v.string()"}
        ]

  ## Returns
  A keyword list of `{type_module, valibot_schema_string}` tuples, or an empty
  list if not configured.
  """
  def valibot_mapping_overrides do
    Application.get_env(:ash_typescript, :valibot_mapping_overrides, [])
  end

  @doc """
  Gets the Effect Schema overrides for custom types from application configuration.

  The Effect counterpart to `zod_mapping_overrides/0`.

  ## Configuration

      config :ash_typescript,
        effect_mapping_overrides: [
          {AshObjectIds.Type, "Schema.String"}
        ]

  ## Returns
  A keyword list of `{type_module, effect_schema_string}` tuples, or an empty
  list if not configured.
  """
  def effect_mapping_overrides do
    Application.get_env(:ash_typescript, :effect_mapping_overrides, [])
  end

  @doc """
  Gets extra imports to inject into the generated Zod schema file.

  Use this alongside `zod_mapping_overrides/0` when an override should reference
  a schema you authored in TypeScript rather than an inline `z.*` expression.
  Same shape as `import_into_generated`, but scoped to the Zod file so unrelated
  imports (hooks, custom types) don't leak into it.

  ## Configuration

      config :ash_typescript,
        zod_import_into_generated: [
          %{import_name: "CustomZodSchemas", file: "assets/js/customZodSchemas.ts"}
        ],
        zod_mapping_overrides: [
          {MyApp.ObjectId, "CustomZodSchemas.objectId"}
        ]

  ## Returns
  A list of `%{import_name: String.t(), file: String.t()}` maps, or an empty list.
  """
  def zod_import_into_generated do
    Application.get_env(:ash_typescript, :zod_import_into_generated, [])
  end

  @doc """
  Gets extra imports to inject into the generated Valibot schema file.

  The Valibot counterpart to `zod_import_into_generated/0`.

  ## Configuration

      config :ash_typescript,
        valibot_import_into_generated: [
          %{import_name: "CustomValibotSchemas", file: "assets/js/customValibotSchemas.ts"}
        ],
        valibot_mapping_overrides: [
          {MyApp.ObjectId, "CustomValibotSchemas.objectId"}
        ]

  ## Returns
  A list of `%{import_name: String.t(), file: String.t()}` maps, or an empty list.
  """
  def valibot_import_into_generated do
    Application.get_env(:ash_typescript, :valibot_import_into_generated, [])
  end

  @doc """
  Gets extra imports to inject into the generated Effect schema file.

  The Effect counterpart to `zod_import_into_generated/0`.

  ## Configuration

      config :ash_typescript,
        effect_import_into_generated: [
          %{import_name: "CustomEffectSchemas", file: "assets/js/customEffectSchemas.ts"}
        ],
        effect_mapping_overrides: [
          {MyApp.ObjectId, "CustomEffectSchemas.objectId"}
        ]

  ## Returns
  A list of `%{import_name: String.t(), file: String.t()}` maps, or an empty list.
  """
  def effect_import_into_generated do
    Application.get_env(:ash_typescript, :effect_import_into_generated, [])
  end

  @doc """
  Gets the Zod import path for generated TypeScript.

  This determines the import statement used in generated TypeScript files.
  Defaults to "zod" for standard npm package.
  Can be customized for different package managers or custom Zod builds.
  """
  def zod_import_path do
    Application.get_env(:ash_typescript, :zod_import_path, "zod")
  end

  @doc """
  Gets the suffix used for generated Zod schema constants.

  This determines the naming pattern for Zod schemas.
  Defaults to "ZodSchema" (e.g., createTodoZodSchema).
  """
  def zod_schema_suffix do
    Application.get_env(:ash_typescript, :zod_schema_suffix, "ZodSchema")
  end

  @doc """
  Determines if Valibot schema generation is enabled.

  When true, generates Valibot schemas alongside TypeScript types for runtime validation.
  Defaults to false (opt-in feature).
  """
  def generate_valibot_schemas? do
    Application.get_env(:ash_typescript, :generate_valibot_schemas, false)
  end

  @doc """
  Gets the Valibot import path for generated TypeScript.

  This determines the import statement used in generated TypeScript files.
  Defaults to "valibot" for standard npm package.
  Can be customized for different package managers or custom Valibot builds.
  """
  def valibot_import_path do
    Application.get_env(:ash_typescript, :valibot_import_path, "valibot")
  end

  @doc """
  Gets the suffix used for generated Valibot schema constants.

  This determines the naming pattern for Valibot schemas.
  Defaults to "ValibotSchema" (e.g., createTodoValibotSchema).
  """
  def valibot_schema_suffix do
    Application.get_env(:ash_typescript, :valibot_schema_suffix, "ValibotSchema")
  end

  @doc """
  Determines if Effect Schema generation is enabled.

  When true, generates Effect v4 schemas alongside TypeScript types for runtime
  validation. Defaults to false (opt-in feature).
  """
  def generate_effect_schemas? do
    Application.get_env(:ash_typescript, :generate_effect_schemas, false)
  end

  @doc """
  Gets the Effect import path for generated TypeScript.

  The generated schema file imports `{ Schema }` from this path.
  Defaults to "effect", the package that ships Schema in Effect v4.
  """
  def effect_import_path do
    Application.get_env(:ash_typescript, :effect_import_path, "effect")
  end

  @doc """
  Gets the suffix used for generated Effect schema constants.

  Defaults to "EffectSchema" (e.g., createTodoEffectSchema).
  """
  def effect_schema_suffix do
    Application.get_env(:ash_typescript, :effect_schema_suffix, "EffectSchema")
  end

  @doc """
  Determines if Phoenix channel-based RPC actions should be generated.

  This controls whether channel functions are generated alongside fetch-based functions.
  Defaults to false.
  """
  def generate_phx_channel_rpc_actions? do
    Application.get_env(:ash_typescript, :generate_phx_channel_rpc_actions, false)
  end

  @doc """
  Determines whether to generate validation functions alongside RPC functions.

  This controls whether validation functions are generated alongside fetch-based functions.
  Defaults to false.
  """
  def generate_validation_functions? do
    Application.get_env(:ash_typescript, :generate_validation_functions, false)
  end

  @doc """
  Gets the global default for not_found_error? behavior on get actions.

  When true (default), get actions return an error if no record is found.
  When false, get actions return null instead of an error.

  Individual rpc_action configurations can override this global default.
  """
  def not_found_error? do
    Application.get_env(:ash_typescript, :not_found_error?, true)
  end

  @doc """
  Checks if RPC action hooks are enabled (either beforeRequest or afterRequest).

  Returns true if either beforeRequest or afterRequest hook is configured for RPC actions.
  """
  def rpc_action_hooks_enabled? do
    AshTypescript.rpc_action_before_request_hook() != nil ||
      AshTypescript.rpc_action_after_request_hook() != nil
  end

  @doc """
  Checks if RPC validation hooks are enabled (either beforeRequest or afterRequest).

  Returns true if either beforeRequest or afterRequest hook is configured for validations.
  """
  def rpc_validation_hooks_enabled? do
    AshTypescript.rpc_validation_before_request_hook() != nil ||
      AshTypescript.rpc_validation_after_request_hook() != nil
  end

  @doc """
  Checks if RPC action channel hooks are enabled (either beforeChannelPush or afterChannelResponse).

  Returns true if either beforeChannelPush or afterChannelResponse hook is configured for RPC actions.
  """
  def rpc_action_channel_hooks_enabled? do
    AshTypescript.rpc_action_before_channel_push_hook() != nil ||
      AshTypescript.rpc_action_after_channel_response_hook() != nil
  end

  @doc """
  Checks if RPC validation channel hooks are enabled (either beforeChannelPush or afterChannelResponse).

  Returns true if either beforeChannelPush or afterChannelResponse hook is configured for validations.
  """
  def rpc_validation_channel_hooks_enabled? do
    AshTypescript.rpc_validation_before_channel_push_hook() != nil ||
      AshTypescript.rpc_validation_after_channel_response_hook() != nil
  end

  @doc """
  Gets the configured manifest file path, or nil if manifest generation is disabled.

  When set to a file path, generates a MANIFEST.md file at that location.
  When nil (default), manifest generation is disabled.

  Example config:
      config :ash_typescript, manifest_file: "./test/ts/MANIFEST.md"
  """
  def manifest_file do
    Application.get_env(:ash_typescript, :manifest_file)
  end

  @doc """
  Gets the configured path for the JSON manifest file.

  When set, `mix ash_typescript.codegen` generates a machine-readable JSON manifest
  alongside the TypeScript output. This manifest contains structured metadata about
  every RPC action (function names, types, pagination, variants) that third-party
  packages can consume to generate typed wrappers (e.g., TanStack Query integrations).

  Example config:
      config :ash_typescript, json_manifest_file: "./assets/js/ash_rpc_manifest.json"
  """
  def json_manifest_file do
    Application.get_env(:ash_typescript, :json_manifest_file)
  end

  @doc """
  Controls how `filename` is formatted in the JSON manifest `files` entries.

  - `:relative` (default) — path relative to the manifest file (e.g., `./generated.ts`, `../other/file.ts`)
  - `:absolute` — absolute filesystem path (e.g., `/home/user/app/assets/js/generated.ts`)
  - `:basename` — bare filename only (e.g., `generated.ts`)

  Example config:
      config :ash_typescript, json_manifest_filename_format: :relative
  """
  def json_manifest_filename_format do
    Application.get_env(:ash_typescript, :json_manifest_filename_format, :relative)
  end

  @doc """
  Gets the Phoenix import path for generated TypeScript.

  This determines the import statement used for Phoenix channels in generated TypeScript files.
  Defaults to "phoenix" for standard npm package.
  Can be customized for different package managers or custom Phoenix builds.
  """
  def phoenix_import_path do
    Application.get_env(:ash_typescript, :phoenix_import_path, "phoenix")
  end

  @doc """
  Determines if namespace file generation is enabled.

  When true, namespaced RPC actions are generated into separate files.
  When false (default), all RPC functions are in a single file.
  """
  def enable_namespace_files? do
    Application.get_env(:ash_typescript, :enable_namespace_files, false)
  end

  @doc """
  Gets the output directory for namespace files.

  When nil, namespace files are written to the same directory as the main output file.
  """
  def namespace_output_dir do
    Application.get_env(:ash_typescript, :namespace_output_dir)
  end

  @doc """
  Determines if Ash internals should be included in JSDoc comments.

  When true, JSDoc includes @resource (Elixir module), @internalActionName (Ash action name),
  and the action's description if present.
  When false (default), only the default description, @actionType, and @namespace are included.
  """
  def add_ash_internals_to_jsdoc? do
    Application.get_env(:ash_typescript, :add_ash_internals_to_jsdoc, false)
  end

  @doc """
  Determines if Ash internals should be included in the manifest file.

  When true, manifest includes resource module paths, internal Ash action names,
  and the action's description if present.
  When false (default), only public-facing info is included (rpc_action descriptions,
  default descriptions).
  """
  def add_ash_internals_to_manifest? do
    Application.get_env(:ash_typescript, :add_ash_internals_to_manifest, false)
  end

  @doc """
  Determines if a resource requires a tenant parameter.

  A resource requires a tenant if it has multitenancy configured and global? is false (default).
  """
  def requires_tenant?(resource) do
    case AshTypescript.resource_lookup() |> Map.get(resource) do
      %Ash.Info.Manifest.Resource{multitenancy: %{strategy: strategy, global?: global?}}
      when strategy in [:attribute, :context] ->
        not global?

      _ ->
        false
    end
  end

  @doc """
  Determines if a resource should have tenant parameters in the generated TypeScript interface.

  This combines resource multitenancy requirements with the configuration setting.
  """
  def requires_tenant_parameter?(resource) do
    requires_tenant?(resource) and require_tenant_parameters?()
  end

  @doc """
  Main entry point for the new RPC processing pipeline.

  ## Parameters
  - `otp_app` - The OTP application atom. Retained for API compatibility; it does
    **not** scope action discovery. Actions are resolved from the single manifest
    in `config :ash_typescript, manifest:`, so passing a different `otp_app` does
    not restrict — or widen — the set of reachable actions.
  - `conn` - The Plug connection
  - `params` - Request parameters map

  ## Returns
  A client-formatted response map (never an ok/error tuple):
  - `%{"success" => true, "data" => data}` on success
  - `%{"success" => false, "errors" => [error, ...]}` on failure

  Key casing follows the configured `output_field_formatter`.

  ## Error Handling
  This implementation uses strict validation and fails fast on any invalid input.
  No permissive modes - all errors are reported immediately.
  """
  @spec run_action(atom(), Plug.Conn.t(), map()) :: map()
  def run_action(otp_app, conn, params) do
    with {:ok, parsed_request} <- Pipeline.parse_request(otp_app, conn, params),
         {:ok, ash_result} <- Pipeline.execute_ash_action(parsed_request),
         {:ok, processed_result} <- Pipeline.process_result(ash_result, parsed_request) do
      Pipeline.format_output(%{success: true, data: processed_result}, parsed_request)
    else
      {:error, reason} -> error_response(reason)
    end
  end

  @doc """
  Validates action parameters without execution.
  Used for form validation in the client.

  Returns the same client-formatted response map shape as `run_action/3`, and
  treats `otp_app` the same way — it does not scope action discovery.
  """
  @spec validate_action(atom(), Plug.Conn.t(), map()) :: map()
  def validate_action(otp_app, conn, params) do
    case Pipeline.parse_request(otp_app, conn, params, validation_mode?: true) do
      {:ok, parsed_request} ->
        validate_form_input(parsed_request)
        |> Pipeline.format_output(parsed_request)

      {:error, reason} ->
        error_response(reason)
    end
  end

  defp validate_form_input(%{action: action, resource: resource, input: input} = request) do
    opts = [
      actor: request.actor,
      tenant: request.tenant,
      context: request.context
    ]

    case action.type do
      :read ->
        # For read actions, validate by building a query
        validate_read_action(request, input, opts)

      action_type when action_type in [:update, :destroy] ->
        # Load the target with the RPC action's configured read_action so
        # validation resolves the same record set as execution. Without :action,
        # Ash falls back to the primary read action, turning this into an
        # existence/field oracle for records read_action is meant to hide.
        get_opts =
          case request.rpc_action.read_action do
            nil -> opts
            read_action -> Keyword.put(opts, :action, read_action)
          end

        case Ash.get(resource, request.identity, get_opts) do
          {:ok, record} ->
            perform_form_validation(record, action.name, input, opts, request)

          {:error, error} ->
            errors =
              Errors.to_errors(error, request.domain, resource, action.name, request.context)

            %{success: false, errors: errors}
        end

      _ ->
        perform_form_validation(resource, action.name, input, opts, request)
    end
  end

  defp validate_read_action(
         %{resource: resource, action: action, domain: domain, context: context},
         input,
         opts
       ) do
    # For read actions, just validate the input against the action definition
    # get_by validation is handled in parse_get_by during request parsing
    query =
      resource
      |> Ash.Query.for_read(action.name, input, opts)

    case query do
      %Ash.Query{errors: []} ->
        %{success: true}

      %Ash.Query{errors: errors} when errors != [] ->
        formatted_errors = Errors.to_errors(errors, domain, resource, action.name, context)
        %{success: false, errors: formatted_errors}

      _ ->
        %{success: true}
    end
  rescue
    e ->
      formatted_errors = Errors.to_errors(e, domain, resource, action.name, context)
      %{success: false, errors: formatted_errors}
  end

  defp perform_form_validation(record_or_resource, action_name, input, opts, %{
         domain: domain,
         resource: resource,
         context: context
       }) do
    form =
      record_or_resource
      |> AshPhoenix.Form.for_action(action_name, opts)
      |> AshPhoenix.Form.validate(input)

    form_errors = AshPhoenix.Form.errors(form)

    if Enum.empty?(form_errors) do
      %{success: true}
    else
      # Convert form errors to exceptions/error classes for proper handling
      errors =
        form_errors
        |> Enum.map(fn {field, messages} ->
          messages
          |> List.wrap()
          |> Enum.map(fn message ->
            # Create a validation error structure
            %Ash.Error.Changes.InvalidAttribute{
              field: field,
              message: to_string(message),
              path: [field]
            }
          end)
        end)
        |> List.flatten()

      formatted_errors = Errors.to_errors(errors, domain, resource, action_name, context)
      %{success: false, errors: formatted_errors}
    end
  end

  @doc """
  Runs a typed query for server-side rendering and data fetching.

  This function looks up a typed query by name and executes it with the configured fields,
  returning the data in the exact shape defined by the typed query. This is ideal for
  SSR controllers that need to pre-fetch data with type safety.

  ## Parameters
  - `otp_app` - The OTP application name. Retained for API compatibility; it does
    **not** scope typed-query discovery (see `run_action/3`). Queries are resolved
    from the configured manifest.
  - `typed_query_name` - The atom name of the typed query to execute
  - `params` - Map with optional `:input` and `:page` keys
  - `conn` - The Plug connection (for tenant context, etc.)

  ## Returns
  A client-formatted response map (same shape as `run_action/3`):
  - `%{"success" => true, "data" => data}` on success
  - `%{"success" => false, "errors" => [error, ...]}` on failure (including
    when the typed query name is not found)

  Key casing follows the configured `output_field_formatter`.

  ## Example
      # In a Phoenix controller
      def index(conn, _params) do
        case AshTypescript.Rpc.run_typed_query(:my_app, :list_todos_user_page, %{}, conn) do
          %{"success" => true, "data" => todos} ->
            render(conn, "index.html", initial_todos: todos)
          %{"success" => false, "errors" => errors} ->
            # Handle errors appropriately
            send_resp(conn, 500, "Error loading data")
        end
      end
  """
  @spec run_typed_query(atom(), atom(), map(), Plug.Conn.t()) :: map()
  def run_typed_query(otp_app, typed_query_name, params \\ %{}, conn) do
    case Pipeline.find_typed_query(typed_query_name) do
      nil ->
        error_response({:typed_query_not_found, typed_query_name})

      entrypoint ->
        typed_query = AshTypescript.Manifest.Custom.typed_query(entrypoint)

        %{
          "typed_query_action" => Atom.to_string(typed_query_name),
          "fields" => typed_query.fields
        }
        |> maybe_add_param("input", params[:input])
        |> maybe_add_param("page", params[:page])
        |> maybe_add_param("filter", params[:filter])
        |> maybe_add_param("sort", params[:sort])
        |> then(&run_action(otp_app, conn, &1))
    end
  end

  # Builds the client-formatted failure response for a pipeline error reason.
  # ErrorBuilder returns either a single error or a list of them.
  defp error_response(reason) do
    errors = reason |> ErrorBuilder.build_error_response() |> List.wrap()

    Pipeline.format_output(%{success: false, errors: errors})
  end

  defp maybe_add_param(params, _key, nil), do: params
  defp maybe_add_param(params, key, value), do: Map.put(params, key, value)
end
