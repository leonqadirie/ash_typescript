# SPDX-FileCopyrightText: 2025 ash_typescript contributors <https://github.com/ash-project/ash_typescript/graphs.contributors>
#
# SPDX-License-Identifier: MIT

defmodule AshTypescript.Rpc.Codegen.ManifestGenerator do
  @moduledoc """
  Generates a Markdown manifest of all RPC actions for discoverability.

  The manifest provides an overview of all available RPC functions, their types,
  and associated TypeScript artifacts (validation functions, Zod schemas, channel functions).

  Supports grouping by namespace when namespaces are configured, and includes
  detailed information like descriptions, deprecation notices, and related action references.
  """

  @tc_mutation_methods [:post, :patch, :put, :delete]

  alias AshTypescript.Codegen.SchemaCore
  alias AshTypescript.Codegen.SchemaFormatter
  alias AshTypescript.Helpers
  alias AshTypescript.Rpc.Codegen.FunctionNames
  alias AshTypescript.Rpc.Codegen.RpcConfigCollector

  @doc """
  Generates a Markdown manifest of all RPC actions.

  Returns a string containing the complete manifest in Markdown format.

  ## Options
  The manifest respects the following configuration:
  - `add_ash_internals_to_manifest` - When true, includes Elixir module paths and internal action names
  """
  def generate_manifest do
    include_internals? = AshTypescript.Rpc.add_ash_internals_to_manifest?()
    entrypoints = AshTypescript.entrypoints()

    # Get namespaced actions to determine if we should group by namespace
    namespaced_actions = RpcConfigCollector.get_rpc_resources_by_namespace(entrypoints)

    has_namespaces? = has_meaningful_namespaces?(namespaced_actions)

    date = Date.utc_today() |> Date.to_string()

    content =
      if has_namespaces? do
        generate_namespace_grouped_content(namespaced_actions, include_internals?)
      else
        generate_domain_grouped_content(entrypoints, include_internals?)
      end

    tc_section = generate_typed_controller_manifest_section()

    """
    # RPC Action Manifest

    Generated: #{date}

    #{content}
    #{tc_section}
    """
    |> String.trim_trailing()
    |> Kernel.<>("\n")
  end

  defp has_meaningful_namespaces?(namespaced_actions) do
    namespaced_actions
    |> Map.keys()
    |> Enum.any?(&(&1 != nil))
  end

  defp generate_namespace_grouped_content(namespaced_actions, include_internals?) do
    # Sort namespaces: nil first (as "Default"), then alphabetically
    sorted_namespaces =
      namespaced_actions
      |> Map.keys()
      |> Enum.sort_by(fn
        nil -> {0, ""}
        ns -> {1, ns}
      end)

    sorted_namespaces
    |> Enum.map_join("\n", fn namespace ->
      actions = namespaced_actions[namespace]
      generate_namespace_section(namespace, actions, include_internals?)
    end)
  end

  defp generate_namespace_section(namespace, actions, include_internals?) do
    section_title =
      case namespace do
        nil -> "Default (No Namespace)"
        ns -> "Namespace: #{ns}"
      end

    actions_by_resource =
      actions
      |> Enum.group_by(fn {resource, _action, _rpc_action, _domain, _resource_config} ->
        resource
      end)
      |> Enum.sort_by(fn {resource, _} -> resource_short_name(resource) end)

    resource_sections =
      actions_by_resource
      |> Enum.map_join("\n", fn {resource, resource_actions} ->
        generate_resource_actions_section(resource, resource_actions, include_internals?)
      end)

    """
    ## #{section_title}

    #{resource_sections}
    """
  end

  defp generate_resource_actions_section(resource, actions, include_internals?) do
    resource_name = resource_short_name(resource)

    sorted_actions =
      actions
      |> Enum.sort_by(fn {_resource, _action, rpc_action, _domain, _resource_config} ->
        Atom.to_string(rpc_action.name)
      end)

    table = generate_actions_table_from_tuples(sorted_actions, include_internals?)

    typed_queries =
      actions
      |> Enum.flat_map(fn {_resource, _action, _rpc_action, _domain, resource_config} ->
        Map.get(resource_config, :typed_queries, [])
      end)
      |> Enum.uniq_by(& &1.name)
      |> Enum.sort_by(fn typed_query -> Atom.to_string(typed_query.name) end)

    typed_queries_section = generate_typed_queries_section(typed_queries)

    """
    ### #{resource_name}

    #{table}
    #{typed_queries_section}
    """
    |> String.trim_trailing()
  end

  # Generate content grouped by domain (original behavior)
  defp generate_domain_grouped_content(entrypoints, include_internals?) do
    domain_configs = RpcConfigCollector.get_rpc_config_by_domain(entrypoints)

    sorted_domains =
      domain_configs
      |> Enum.sort_by(fn {domain, _config} -> inspect(domain) end)

    sorted_domains
    |> Enum.map_join("\n", fn {domain, rpc_config} ->
      generate_domain_section(domain, rpc_config, include_internals?)
    end)
  end

  defp generate_domain_section(domain, rpc_config, include_internals?) do
    domain_name = inspect(domain)

    sorted_resources =
      rpc_config
      |> Enum.filter(fn %{rpc_actions: rpc_actions} -> rpc_actions != [] end)
      |> Enum.sort_by(fn %{resource: resource} -> resource_short_name(resource) end)

    resource_sections =
      sorted_resources
      |> Enum.map_join("\n", fn resource_config ->
        generate_resource_section(domain, resource_config, include_internals?)
      end)

    """
    ## #{domain_name}

    #{resource_sections}
    """
  end

  defp generate_resource_section(
         domain,
         %{
           resource: resource,
           rpc_actions: rpc_actions,
           typed_queries: typed_queries
         } = resource_config,
         include_internals?
       ) do
    resource_name = resource_short_name(resource)
    action_lookup = AshTypescript.action_lookup()

    sorted_actions =
      rpc_actions
      |> Enum.sort_by(fn rpc_action -> Atom.to_string(rpc_action.name) end)

    sorted_typed_queries =
      typed_queries
      |> Enum.sort_by(fn typed_query -> Atom.to_string(typed_query.name) end)

    actions_table =
      generate_actions_table(
        action_lookup,
        resource,
        sorted_actions,
        domain,
        resource_config,
        include_internals?
      )

    typed_queries_section = generate_typed_queries_section(sorted_typed_queries)

    """
    ### #{resource_name}

    #{actions_table}
    #{typed_queries_section}
    """
    |> String.trim_trailing()
  end

  defp generate_actions_table(
         action_lookup,
         resource,
         rpc_actions,
         domain,
         resource_config,
         include_internals?
       ) do
    show_validation = AshTypescript.Rpc.generate_validation_functions?()
    show_channel = AshTypescript.Rpc.generate_phx_channel_rpc_actions?()
    schema_formatters = SchemaFormatter.enabled()

    headers = build_headers(show_validation, schema_formatters, show_channel, include_internals?)

    separator =
      build_separator(show_validation, schema_formatters, show_channel, include_internals?)

    rows =
      rpc_actions
      |> Enum.map_join("\n", fn rpc_action ->
        action = Map.get(action_lookup, {resource, rpc_action.action})
        namespace = RpcConfigCollector.resolve_namespace(domain, resource_config, rpc_action)

        build_row(
          resource,
          action,
          rpc_action,
          namespace,
          show_validation,
          schema_formatters,
          show_channel,
          include_internals?
        )
      end)

    details =
      rpc_actions
      |> Enum.map(fn rpc_action ->
        action = Map.get(action_lookup, {resource, rpc_action.action})
        namespace = RpcConfigCollector.resolve_namespace(domain, resource_config, rpc_action)
        build_action_details(resource, action, rpc_action, namespace, include_internals?)
      end)
      |> Enum.reject(&is_nil/1)
      |> Enum.join("\n")

    table = """
    #{headers}
    #{separator}
    #{rows}
    """

    if details != "" do
      table <> "\n" <> details
    else
      table
    end
  end

  defp generate_actions_table_from_tuples(actions, include_internals?) do
    show_validation = AshTypescript.Rpc.generate_validation_functions?()
    show_channel = AshTypescript.Rpc.generate_phx_channel_rpc_actions?()
    schema_formatters = SchemaFormatter.enabled()

    headers = build_headers(show_validation, schema_formatters, show_channel, include_internals?)

    separator =
      build_separator(show_validation, schema_formatters, show_channel, include_internals?)

    rows =
      actions
      |> Enum.map_join("\n", fn {resource, action, rpc_action, domain, resource_config} ->
        namespace = RpcConfigCollector.resolve_namespace(domain, resource_config, rpc_action)

        build_row(
          resource,
          action,
          rpc_action,
          namespace,
          show_validation,
          schema_formatters,
          show_channel,
          include_internals?
        )
      end)

    details =
      actions
      |> Enum.map(fn {resource, action, rpc_action, domain, resource_config} ->
        namespace = RpcConfigCollector.resolve_namespace(domain, resource_config, rpc_action)
        build_action_details(resource, action, rpc_action, namespace, include_internals?)
      end)
      |> Enum.reject(&is_nil/1)
      |> Enum.join("\n")

    table = """
    #{headers}
    #{separator}
    #{rows}
    """

    if details != "" do
      table <> "\n" <> details
    else
      table
    end
  end

  defp build_headers(show_validation, schema_formatters, show_channel, include_internals?) do
    "| Function | Action Type |"
    |> maybe_append(" Ash Action |", include_internals?)
    |> maybe_append(" Resource |", include_internals?)
    |> maybe_append(" Validation |", show_validation)
    |> append_cells(schema_headers(schema_formatters))
    |> maybe_append(" Channel |", show_channel)
    |> maybe_append(" Validation Channel |", show_validation and show_channel)
  end

  defp build_separator(show_validation, schema_formatters, show_channel, include_internals?) do
    "|----------|-------------|"
    |> maybe_append("------------|", include_internals?)
    |> maybe_append("----------|", include_internals?)
    |> maybe_append("------------|", show_validation)
    |> append_separators(schema_headers(schema_formatters))
    |> maybe_append("---------|", show_channel)
    |> maybe_append("--------------------|", show_validation and show_channel)
  end

  defp build_row(
         resource,
         action,
         rpc_action,
         _namespace,
         show_validation,
         schema_formatters,
         show_channel,
         include_internals?
       ) do
    rpc_action_name = to_string(rpc_action.name)
    function_name = FunctionNames.execution(rpc_action_name)
    action_type = Atom.to_string(action.type)
    action_name = Atom.to_string(rpc_action.action)
    resource_module = inspect(resource)

    validate_name = FunctionNames.validation(rpc_action_name)
    channel_name = FunctionNames.channel(rpc_action_name)
    validate_channel_name = FunctionNames.validation_channel(rpc_action_name)

    # An action with no inputs gets no schema, so naming one here would point
    # readers at an export that does not exist. Names come from SchemaCore so
    # the configurable suffixes cannot drift.
    has_schema? = SchemaCore.action_has_schema?(action)

    schema_cells =
      Enum.map(schema_formatters, fn formatter ->
        if has_schema?,
          do: "`#{SchemaCore.action_schema_name(formatter, rpc_action_name)}`",
          else: "-"
      end)

    "| `#{function_name}` | #{action_type} |"
    |> maybe_append(" `#{action_name}` |", include_internals?)
    |> maybe_append(" `#{resource_module}` |", include_internals?)
    |> maybe_append(" `#{validate_name}` |", show_validation)
    |> append_cells(schema_cells)
    |> maybe_append(" `#{channel_name}` |", show_channel)
    |> maybe_append(" `#{validate_channel_name}` |", show_validation and show_channel)
  end

  defp build_action_details(resource, action, rpc_action, namespace, include_internals?) do
    function_name = FunctionNames.execution(rpc_action.name)
    resource_name = resource |> Module.split() |> List.last()

    description = get_description(rpc_action, action, resource_name, include_internals?)
    deprecated = get_deprecated_text(rpc_action)
    see_refs = get_see_refs(rpc_action)
    namespace_text = if namespace, do: "**Namespace:** `#{namespace}`", else: nil

    details =
      [description, deprecated, see_refs, namespace_text]
      |> Enum.reject(&is_nil/1)

    if Enum.empty?(details) do
      nil
    else
      details_text = Enum.join(details, " | ")
      "- **`#{function_name}`**: #{details_text}"
    end
  end

  defp get_description(rpc_action, action, resource_name, include_internals?) do
    rpc_description = Map.get(rpc_action, :description)
    action_description = Map.get(action, :description)

    cond do
      is_binary(rpc_description) and rpc_description != "" ->
        rpc_description

      include_internals? and is_binary(action_description) and action_description != "" ->
        action_description

      true ->
        default_description(action.type, resource_name)
    end
  end

  defp get_deprecated_text(rpc_action) do
    case Map.get(rpc_action, :deprecated) do
      nil -> nil
      false -> nil
      true -> "⚠️ **Deprecated**"
      message when is_binary(message) -> "⚠️ **Deprecated:** #{message}"
    end
  end

  defp get_see_refs(rpc_action) do
    see_list = Map.get(rpc_action, :see) || []

    if Enum.empty?(see_list) do
      nil
    else
      refs =
        Enum.map_join(see_list, ", ", fn action_name ->
          "`#{FunctionNames.execution(action_name)}`"
        end)

      "**See also:** #{refs}"
    end
  end

  defp default_description(:read, resource_name), do: "Read #{resource_name} records"
  defp default_description(:create, resource_name), do: "Create a new #{resource_name}"
  defp default_description(:update, resource_name), do: "Update an existing #{resource_name}"
  defp default_description(:destroy, resource_name), do: "Delete a #{resource_name}"

  defp default_description(:action, resource_name),
    do: "Execute generic action on #{resource_name}"

  defp maybe_append(string, suffix, true), do: string <> suffix
  defp maybe_append(string, _suffix, false), do: string

  defp generate_typed_queries_section([]), do: ""

  defp generate_typed_queries_section(typed_queries) do
    items =
      typed_queries
      |> Enum.map_join("\n", fn typed_query ->
        const_name =
          typed_query.ts_fields_const_name || Helpers.format_output_field(typed_query.name)

        # Type names are always PascalCase in TypeScript
        type_name =
          typed_query.ts_result_type_name ||
            "#{Helpers.snake_to_pascal_case(typed_query.name)}Result"

        description = Map.get(typed_query, :description)

        if is_binary(description) and description != "" do
          "- `#{const_name}` → `#{type_name}`: #{description}"
        else
          "- `#{const_name}` → `#{type_name}`"
        end
      end)

    """

    **Typed Queries:**
    #{items}
    """
  end

  defp resource_short_name(resource) do
    resource
    |> Module.split()
    |> List.last()
  end

  defp generate_typed_controller_manifest_section do
    routes_config =
      AshTypescript.TypedController.Codegen.RouteConfigCollector.get_typed_controllers()

    if routes_config == [] do
      ""
    else
      router = AshTypescript.router()

      route_infos =
        AshTypescript.TypedController.Codegen.resolve_route_infos(router, routes_config)

      schema_formatters = SchemaFormatter.enabled()

      headers = build_tc_headers(schema_formatters)
      separator = build_tc_separator(schema_formatters)

      sorted_infos =
        Enum.sort_by(route_infos, fn info ->
          {info.scope_prefix || "", info.route.name}
        end)

      rows =
        sorted_infos
        |> Enum.map_join("\n", fn info -> build_tc_row(info, schema_formatters) end)

      """

      ## Typed Controller Routes

      #{headers}
      #{separator}
      #{rows}
      """
      |> String.trim_trailing()
    end
  end

  defp build_tc_headers(schema_formatters) do
    "| Method | Path | Function | Input Type | Result Type |"
    |> append_cells(schema_headers(schema_formatters))
  end

  defp build_tc_separator(schema_formatters) do
    "|--------|------|----------|------------|-------------|"
    |> append_separators(schema_headers(schema_formatters))
  end

  defp build_tc_row(info, schema_formatters) do
    method = info.method |> to_string() |> String.upcase()
    path = info.path || ""

    function_name = AshTypescript.TypedController.Codegen.route_function_name(info)

    path_param_set = MapSet.new(info.path_params)

    input_args =
      info.route.arguments
      |> Enum.reject(fn arg -> MapSet.member?(path_param_set, arg.name) end)

    # The named input type only exists for mutation fetch functions (so not in
    # :paths_only mode); validation schemas are rendered for any route with
    # non-path arguments, GET included.
    input_type =
      if info.method in @tc_mutation_methods and input_args != [] and
           AshTypescript.TypedController.Codegen.fetch_function?(info) do
        AshTypescript.TypedController.Codegen.route_input_type_name(
          info.route,
          info.scope_prefix
        )
      else
        "-"
      end

    result_type =
      if info.route.returns do
        AshTypescript.TypedController.Codegen.route_result_type_name(
          info.route,
          info.scope_prefix
        )
      else
        "-"
      end

    schema_cells =
      Enum.map(schema_formatters, fn formatter ->
        if input_args == [] do
          "-"
        else
          name =
            AshTypescript.TypedController.Codegen.route_schema_name(
              formatter,
              info.route,
              info.scope_prefix
            )

          "`#{name}`"
        end
      end)

    "| #{method} | #{path} | `#{function_name}` | #{input_type} | #{result_type} |"
    |> append_cells(schema_cells)
  end

  defp append_cells(line, cells), do: line <> Enum.map_join(cells, &" #{&1} |")

  # Each separator cell spans its header label plus the surrounding spaces.
  defp append_separators(line, labels) do
    line <> Enum.map_join(labels, &(String.duplicate("-", String.length(&1) + 2) <> "|"))
  end

  defp schema_headers(formatters), do: Enum.map(formatters, &"#{&1.library_name()} Schema")
end
