# SPDX-FileCopyrightText: 2025 ash_typescript contributors <https://github.com/ash-project/ash_typescript/graphs/contributors>
#
# SPDX-License-Identifier: MIT

defmodule AshTypescript.Rpc.Codegen do
  @moduledoc """
  Generates TypeScript code for interacting with Ash resources via Rpc.
  """
  alias AshTypescript.Codegen.SchemaCore
  alias AshTypescript.Codegen.SchemaFormatter

  alias AshTypescript.Rpc.Codegen.FunctionGenerators.ChannelRenderer
  alias AshTypescript.Rpc.Codegen.FunctionGenerators.HttpRenderer
  alias AshTypescript.Rpc.Codegen.FunctionGenerators.TypedQueries
  alias AshTypescript.Rpc.Codegen.FunctionNames
  alias AshTypescript.Rpc.Codegen.Helpers.ActionIntrospection
  alias AshTypescript.Rpc.Codegen.RpcConfigCollector
  alias AshTypescript.Rpc.Codegen.TypeGenerators.InputTypes
  alias AshTypescript.Rpc.Codegen.TypeGenerators.ResultTypes
  alias AshTypescript.Rpc.Codegen.TypescriptStatic

  @doc """
  Formats an endpoint configuration for TypeScript code generation.

  Delegates to `AshTypescript.Helpers.format_ts_value/1`.
  """
  def format_endpoint_for_typescript(endpoint) when is_binary(endpoint) do
    "\"#{endpoint}\""
  end

  def format_endpoint_for_typescript({:runtime_expr, expression})
      when is_binary(expression) do
    expression
  end

  @doc """
  The marker comment used to separate generated code from custom code in namespace files.
  Content below this marker is preserved when regenerating namespace files.
  """
  defdelegate namespace_custom_code_marker, to: AshTypescript.Codegen.ImportResolver

  defp collect_action_exports(actions) do
    actions
    |> Enum.flat_map(fn {resource, action, rpc_action, _domain, _res_config} ->
      collect_exports_for_action(resource, action, rpc_action)
    end)
    |> Enum.uniq()
  end

  defp has_optional_pagination?(_resource, action, rpc_action) do
    ash_get? = action.get? || false
    rpc_get? = Map.get(rpc_action, :get?, false)
    rpc_get_by = (Map.get(rpc_action, :get_by) || []) != []
    is_get_action = ash_get? or rpc_get? or rpc_get_by

    action.type == :read and
      not is_get_action and
      ActionIntrospection.action_supports_pagination?(action) and
      not ActionIntrospection.action_requires_pagination?(action)
  end

  defp collect_exports_for_action(resource, action, rpc_action) do
    rpc_action_name = to_string(rpc_action.name)
    function_name = FunctionNames.execution(rpc_action_name)

    exports = [{function_name, :value}]

    has_input? = SchemaCore.action_has_schema?(action)

    exports =
      if has_input? do
        exports ++ [{FunctionNames.input_type(rpc_action_name), :type}]
      else
        exports
      end

    # Schema exports carry their formatter so namespace files re-export each
    # one from that library's schema file.
    exports =
      if has_input? do
        exports ++
          Enum.map(SchemaFormatter.enabled(), fn formatter ->
            {SchemaCore.action_schema_name(formatter, rpc_action_name), {:schema, formatter}}
          end)
      else
        exports
      end

    exports =
      if action.type == :read do
        pascal_name = Macro.camelize(rpc_action_name)

        base_read_exports = [
          {"#{pascal_name}Fields", :type},
          {"Infer#{pascal_name}Result", :type},
          {"#{pascal_name}Result", :type}
        ]

        # Config type is only generated when the action has optional pagination
        # (see type_builders.ex build_optional_pagination_config/2)
        config_export =
          if has_optional_pagination?(resource, action, rpc_action) do
            [{"#{pascal_name}Config", :type}]
          else
            []
          end

        exports ++ base_read_exports ++ config_export
      else
        pascal_name = Macro.camelize(rpc_action_name)
        exports ++ [{"#{pascal_name}Result", :type}]
      end

    exports =
      if AshTypescript.Rpc.generate_validation_functions?() do
        exports ++ [{FunctionNames.validation(rpc_action_name), :value}]
      else
        exports
      end

    if AshTypescript.Rpc.generate_phx_channel_rpc_actions?() do
      exports = exports ++ [{FunctionNames.channel(rpc_action_name), :value}]

      if AshTypescript.Rpc.generate_validation_functions?() do
        exports ++ [{FunctionNames.validation_channel(rpc_action_name), :value}]
      else
        exports
      end
    else
      exports
    end
  end

  @doc """
  Generates RPC-specific TypeScript content for the multi-file architecture.

  Contains hook types, helper functions, typed queries, and RPC functions.
  Types are imported from ash_types.ts via a local `import type` for types
  actually referenced in the body.

  ## Parameters

    * `resources_and_actions` - List of `{resource, action, rpc_action}` tuples
    * `opts` - Hook configuration options (endpoint overrides, lifecycle hooks)
    * `codegen_opts` - Additional codegen options:
      * `:import_paths` - `%{types: path}` for import resolution
      * `:otp_app` - The OTP application name
      * `:all_resources` - All resources for typed query generation
      * `:shared_type_names` - List of type names exported by ash_types.ts (for local import)
      * `:output_file` - Output file path
  """
  def generate_rpc_content(resources_and_actions, opts, codegen_opts) do
    endpoint_process =
      Keyword.get(opts, :run_endpoint, "/rpc/run")
      |> format_endpoint_for_typescript()

    endpoint_validate =
      Keyword.get(opts, :validate_endpoint, "/rpc/validate")
      |> format_endpoint_for_typescript()

    hook_config = build_hook_config(opts)
    import_paths = Keyword.fetch!(codegen_opts, :import_paths)
    all_resources = Keyword.fetch!(codegen_opts, :all_resources)
    shared_type_names = Keyword.get(codegen_opts, :shared_type_names, [])
    resource_lookup = AshTypescript.resource_lookup()
    entrypoints = AshTypescript.entrypoints()
    action_lookup = AshTypescript.action_lookup()
    typed_queries = RpcConfigCollector.get_typed_queries(entrypoints, action_lookup)

    body =
      [
        TypescriptStatic.generate_hook_context_types(hook_config),
        TypescriptStatic.generate_helper_functions(
          hook_config,
          endpoint_process,
          endpoint_validate
        ),
        TypedQueries.generate_typed_queries_section(
          typed_queries,
          resources_and_actions,
          all_resources
        ),
        generate_rpc_functions_no_zod(resources_and_actions, resource_lookup)
      ]
      |> Enum.reject(&(&1 == ""))
      |> Enum.join("\n\n")

    shared_imports =
      AshTypescript.Codegen.ImportResolver.build_shared_type_imports(
        import_paths,
        shared_type_names,
        body
      )

    """
    // Generated by AshTypescript - RPC Actions
    // Do not edit this file manually

    #{TypescriptStatic.generate_imports(output_file: Keyword.get(codegen_opts, :output_file))}
    #{shared_imports}

    #{body}
    """
  end

  defp generate_rpc_functions_no_zod(resources_and_actions, resource_lookup) do
    resources_and_actions
    |> Enum.map_join("\n\n", fn {resource, action, rpc_action} ->
      namespace = Map.get(rpc_action, :namespace)
      generate_rpc_function_no_zod({resource, action, rpc_action}, namespace, resource_lookup)
    end)
  end

  defp generate_rpc_function_no_zod({resource, action, rpc_action}, namespace, resource_lookup) do
    rpc_action_name = to_string(rpc_action.name)
    action = augment_action_with_rpc_settings(action, rpc_action, resource)
    render_opts = if namespace, do: [namespace: namespace], else: []

    input_type =
      InputTypes.generate_input_type(resource, action, rpc_action_name, resource_lookup)

    result_type =
      ResultTypes.generate_result_type(
        resource,
        action,
        rpc_action,
        rpc_action_name,
        resource_lookup
      )

    rpc_function =
      HttpRenderer.render_execution_function(
        resource,
        action,
        rpc_action,
        rpc_action_name,
        render_opts
      )

    validation_function =
      if AshTypescript.Rpc.generate_validation_functions?() do
        HttpRenderer.render_validation_function(
          resource,
          action,
          rpc_action,
          rpc_action_name,
          render_opts
        )
      else
        ""
      end

    channel_function =
      if AshTypescript.Rpc.generate_phx_channel_rpc_actions?() do
        ChannelRenderer.render_execution_function(
          resource,
          action,
          rpc_action,
          rpc_action_name,
          render_opts
        )
      else
        ""
      end

    channel_validation_function =
      if AshTypescript.Rpc.generate_validation_functions?() and
           AshTypescript.Rpc.generate_phx_channel_rpc_actions?() do
        ChannelRenderer.render_validation_function(
          resource,
          action,
          rpc_action,
          rpc_action_name,
          render_opts
        )
      else
        ""
      end

    function_parts =
      [rpc_function, validation_function, channel_validation_function, channel_function]
      |> Enum.reject(&(&1 == ""))

    functions_section = Enum.join(function_parts, "\n\n")

    output_parts =
      [input_type, result_type, functions_section]
      |> Enum.reject(&(&1 == ""))

    Enum.join(output_parts, "\n")
    |> String.trim_trailing("\n")
    |> Kernel.<>("\n")
  end

  @doc """
  Generates per-action schemas for all RPC actions using the given formatter.

  Returns a list of schema strings (one per action that has arguments).
  These are meant to be passed to SharedSchemaGenerator as `:additional_schemas`.
  """
  def generate_rpc_schemas(formatter, resources_and_actions) do
    resources_and_actions
    |> Enum.map(fn {resource, action, rpc_action} ->
      rpc_action_name = to_string(rpc_action.name)
      action = augment_action_with_rpc_settings(action, rpc_action, resource)
      SchemaCore.generate_action_schema(formatter, resource, action, rpc_action_name)
    end)
    |> Enum.reject(&(&1 == ""))
  end

  @doc """
  Generates a namespace re-export file for the given namespace and actions.

  Used by the Orchestrator to generate namespace files with proper import paths.
  """
  def generate_namespace_reexport_content(
        namespace,
        actions,
        main_file_path,
        schema_files \\ %{}
      ) do
    namespace_dir = AshTypescript.Rpc.namespace_output_dir() || Path.dirname(main_file_path)
    namespace_file = Path.join(namespace_dir, "#{namespace}.ts")
    exports = collect_action_exports(actions)

    AshTypescript.Codegen.ImportResolver.generate_namespace_reexport_content(
      namespace,
      exports,
      namespace_file,
      main_file_path,
      schema_files
    )
  end

  defp build_hook_config(opts) do
    %{
      rpc_action_before_request_hook:
        Keyword.get(opts, :rpc_action_before_request_hook) ||
          AshTypescript.rpc_action_before_request_hook(),
      rpc_action_after_request_hook:
        Keyword.get(opts, :rpc_action_after_request_hook) ||
          AshTypescript.rpc_action_after_request_hook(),
      rpc_validation_before_request_hook:
        Keyword.get(opts, :rpc_validation_before_request_hook) ||
          AshTypescript.rpc_validation_before_request_hook(),
      rpc_validation_after_request_hook:
        Keyword.get(opts, :rpc_validation_after_request_hook) ||
          AshTypescript.rpc_validation_after_request_hook(),
      rpc_action_hook_context_type:
        Keyword.get(opts, :rpc_action_hook_context_type) ||
          AshTypescript.rpc_action_hook_context_type(),
      rpc_validation_hook_context_type:
        Keyword.get(opts, :rpc_validation_hook_context_type) ||
          AshTypescript.rpc_validation_hook_context_type(),
      rpc_action_before_channel_push_hook:
        Keyword.get(opts, :rpc_action_before_channel_push_hook) ||
          AshTypescript.rpc_action_before_channel_push_hook(),
      rpc_action_after_channel_response_hook:
        Keyword.get(opts, :rpc_action_after_channel_response_hook) ||
          AshTypescript.rpc_action_after_channel_response_hook(),
      rpc_validation_before_channel_push_hook:
        Keyword.get(opts, :rpc_validation_before_channel_push_hook) ||
          AshTypescript.rpc_validation_before_channel_push_hook(),
      rpc_validation_after_channel_response_hook:
        Keyword.get(opts, :rpc_validation_after_channel_response_hook) ||
          AshTypescript.rpc_validation_after_channel_response_hook(),
      rpc_action_channel_hook_context_type:
        Keyword.get(opts, :rpc_action_channel_hook_context_type) ||
          AshTypescript.rpc_action_channel_hook_context_type(),
      rpc_validation_channel_hook_context_type:
        Keyword.get(opts, :rpc_validation_channel_hook_context_type) ||
          AshTypescript.rpc_validation_channel_hook_context_type()
    }
  end

  # Augments the action with RPC-level settings (get?, get_by)
  # This allows TypeScript generators to see the full picture of what the action does
  #
  # Note: get? and get_by no longer add arguments - they are handled separately:
  # - get? just sets action.get? = true to indicate single-record return
  # - get_by stores the fields for generating a separate getBy config field
  defp augment_action_with_rpc_settings(action, rpc_action, _resource) do
    rpc_get? = Map.get(rpc_action, :get?, false)
    rpc_get_by = Map.get(rpc_action, :get_by) || []

    cond do
      rpc_get? ->
        Map.put(action, :get?, true)

      rpc_get_by != [] ->
        action
        |> Map.put(:get?, true)
        |> Map.put(:rpc_get_by_fields, rpc_get_by)

      true ->
        action
    end
  end
end
