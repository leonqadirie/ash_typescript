# SPDX-FileCopyrightText: 2025 ash_typescript contributors <https://github.com/ash-project/ash_typescript/graphs/contributors>
#
# SPDX-License-Identifier: MIT

defmodule AshTypescript.TypedController.Codegen.TypescriptStatic do
  @moduledoc """
  Generates static TypeScript code for typed controller routes.

  This includes:
  - Custom import statements
  - Hook context type definitions
  - TypedControllerConfig interface
  - TypedControllerResponse type (for routes declaring `returns`)
  - executeTypedControllerRequest helper function
  """

  import AshTypescript.Helpers

  alias AshTypescript.Codegen.ImportResolver

  @doc """
  Generates all static TypeScript code for typed controller routes.

  Returns a string containing imports, config interface, and helper function.
  Only generates content when `typed_controller_mode() == :full`.

  ## Options

    * `:base_path` - Base URL prefix for the `_basePath` constant
    * `:output_file` - The target output file path, for resolving custom import paths
  """
  def generate_static_code(opts \\ []) do
    imports = generate_imports(opts)
    base_path_var = generate_base_path_variable(Keyword.get(opts, :base_path, ""))
    hook_context_type = generate_hook_context_type()
    config_interface = generate_config_interface()
    response_type = generate_response_type()
    helper_function = generate_helper_function()

    [imports, base_path_var, hook_context_type, config_interface, response_type, helper_function]
    |> Enum.reject(&(&1 == ""))
    |> Enum.join("\n")
  end

  @doc """
  Generates a `_basePath` constant when a base path is configured.

  Returns an empty string when the base path is `""` (default).
  """
  def generate_base_path_variable(""), do: ""

  def generate_base_path_variable(base_path) do
    formatted = AshTypescript.Helpers.format_ts_value(base_path)
    "const _basePath = #{formatted};\n"
  end

  defp generate_imports(opts) do
    output_file = Keyword.get(opts, :output_file)

    case AshTypescript.typed_controller_import_into_generated() do
      [] ->
        ""

      imports when is_list(imports) ->
        case ImportResolver.resolve_custom_imports(output_file, imports) do
          "" -> ""
          imports_str -> imports_str <> "\n"
        end
    end
  end

  defp generate_hook_context_type do
    if AshTypescript.typed_controller_hooks_enabled?() do
      context_type = AshTypescript.typed_controller_hook_context_type()

      """
      export type TypedControllerHookContext = #{context_type};
      """
    else
      ""
    end
  end

  defp generate_config_interface do
    hook_ctx_field =
      if AshTypescript.typed_controller_hooks_enabled?() do
        "\n  #{formatted_hook_ctx_field()}?: TypedControllerHookContext;"
      else
        ""
      end

    """
    /**
     * Configuration options for typed controller requests
     */
    export interface TypedControllerConfig {
      #{formatted_headers_field()}?: Record<string, string>;
      #{formatted_fetch_options_field()}?: RequestInit;
      #{formatted_custom_fetch_field()}?: (
        input: RequestInfo | URL,
        init?: RequestInit,
      ) => Promise<Response>;#{hook_ctx_field}
    }
    """
  end

  # Discriminated on `ok` so the body is only typed as the route's result once
  # the caller has checked for success — error responses (422 validation
  # errors, 500s, or anything else the handler sends) stay `unknown`.
  defp generate_response_type do
    """
    /**
     * A fetch Response whose JSON body is typed as `T` when `ok` is true
     */
    export type TypedControllerResponse<T> =
      | (Omit<Response, "ok" | "json"> & { ok: true; json(): Promise<T> })
      | (Omit<Response, "ok" | "json"> & { ok: false; json(): Promise<unknown> });
    """
  end

  defp generate_helper_function do
    before_hook = AshTypescript.typed_controller_before_request_hook()
    after_hook = AshTypescript.typed_controller_after_request_hook()

    before_hook_code =
      if before_hook do
        """
            let processedConfig = config || {};
            if (#{before_hook}) {
              processedConfig = await #{before_hook}(actionName, processedConfig);
            }
        """
      else
        """
            const processedConfig = config || {};
        """
      end

    after_hook_code =
      if after_hook do
        """
            if (#{after_hook}) {
              await #{after_hook}(actionName, response, processedConfig);
            }
        """
      else
        ""
      end

    has_hooks = before_hook != nil or after_hook != nil
    action_name_param = if has_hooks, do: "actionName", else: "_actionName"

    headers_field = formatted_headers_field()
    custom_fetch_field = formatted_custom_fetch_field()
    fetch_options_field = formatted_fetch_options_field()

    """
    /**
     * Internal helper function for making typed controller requests
     */
    export async function executeTypedControllerRequest(
      url: string,
      method: string,
      #{action_name_param}: string,
      body: string | undefined,
      config?: TypedControllerConfig,
      defaultHeaders?: Record<string, string>,
    ): Promise<Response> {
    #{before_hook_code}
      const headers: Record<string, string> = {
        ...(body !== undefined ? { "Content-Type": "application/json" } : {}),
        ...defaultHeaders,
        ...processedConfig.#{headers_field},
      };

      const fetchFunction = processedConfig.#{custom_fetch_field} || fetch;
      const fetchInit: RequestInit = {
        ...processedConfig.#{fetch_options_field},
        method,
        headers,
        ...(body !== undefined ? { body } : {}),
      };

      const response = await fetchFunction(url, fetchInit);

    #{after_hook_code}
      return response;
    }
    """
  end
end
