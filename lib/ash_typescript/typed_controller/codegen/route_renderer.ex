# SPDX-FileCopyrightText: 2025 ash_typescript contributors <https://github.com/ash-project/ash_typescript/graphs/contributors>
#
# SPDX-License-Identifier: MIT

defmodule AshTypescript.TypedController.Codegen.RouteRenderer do
  @moduledoc """
  Generates TypeScript functions for typed controller routes.

  - GET routes generate path helper functions.
  - Mutation routes (POST/PATCH/PUT/DELETE) generate typed action functions
    with input types derived from route arguments.
  - Routes declaring `returns` get an exported result type; their action
    functions resolve to a `TypedControllerResponse` typed by it. GET routes
    declaring `returns` also get a fetch function (see `Codegen.fetch_function?/1`).
  """

  import AshTypescript.Helpers, only: [format_output_field: 1]
  import AshTypescript.Codegen.TypeMapper, only: [get_ts_input_type: 1]

  alias Ash.Info.Manifest.Generator.TypeResolver
  alias AshTypescript.Codegen.SchemaCore
  alias AshTypescript.Codegen.TypeMapper
  alias AshTypescript.Codegen.ValibotSchemaGenerator
  alias AshTypescript.Codegen.ZodSchemaGenerator
  alias AshTypescript.TypedController.Codegen

  @mutation_methods [:post, :patch, :put, :delete]

  @doc """
  Renders TypeScript code for a single route without Zod schema.

  Same as `render/1` but skips Zod schema generation (for split-file mode
  where Zod schemas live in ash_zod.ts).

  ## Options

    * `:has_base_path` - When true, prefixes all URL expressions with `${_basePath}`
  """
  def render_no_zod(route_info, opts \\ []) do
    fetch_function =
      cond do
        not Codegen.fetch_function?(route_info) -> ""
        route_info.method in @mutation_methods -> render_action_function(route_info, opts)
        true -> render_get_fetch_function(route_info)
      end

    [render_result_type(route_info), render_path_helper(route_info, opts), fetch_function]
    |> Enum.reject(&(&1 == ""))
    |> Enum.join("\n")
  end

  # Rendered for every route that declares `returns` — GET routes included, so
  # callers fetching a path helper's URL themselves can type the body too.
  defp render_result_type(%{route: %{returns: nil}}), do: ""

  defp render_result_type(%{route: route, scope_prefix: scope_prefix}) do
    type_name = Codegen.route_result_type_name(route, scope_prefix)
    ts_type = TypeMapper.map_route_result_type(route.returns, route.constraints)

    """
    /**
     * Response body of #{format_output_field(route.name)}
     */
    export type #{type_name} = #{ts_type};
    """
  end

  defp render_path_helper(route_info, opts) do
    %{
      route: route,
      path: path,
      method: method,
      path_params: path_params,
      scope_prefix: scope_prefix
    } = route_info

    has_base_path = Keyword.get(opts, :has_base_path, false)
    function_name = build_function_name(route.name, scope_prefix, :path)
    style = AshTypescript.typed_controller_path_params_style()

    path_param_args = build_path_param_args(route, path_params, style)

    is_mutation = method in @mutation_methods
    query_args = if is_mutation, do: [], else: non_path_args(route, path_params)

    {query_param, query_body_lines} = build_query_param_and_body(query_args)

    all_params =
      path_param_args ++
        if(query_param, do: [query_param], else: [])

    params = Enum.join(all_params, ", ")
    jsdoc = build_jsdoc(route, path)

    use_path_prefix = style == :object and path_params != []

    if query_args == [] do
      url_expr = build_url_template(path, path_params, use_path_prefix, has_base_path)

      """
      #{jsdoc}
      export function #{function_name}(#{params}): string {
        return #{url_expr};
      }
      """
    else
      url_expr = build_url_template_to_variable(path, path_params, use_path_prefix, has_base_path)
      body = Enum.join(query_body_lines, "\n")

      """
      #{jsdoc}
      export function #{function_name}(#{params}): string {
        #{url_expr}
      #{body}
        const qs = searchParams.toString();
        return qs ? `${base}?${qs}` : base;
      }
      """
    end
  end

  defp get_path_param_type(route, param) do
    case Enum.find(route.arguments, &(&1.name == param)) do
      nil -> "string"
      arg -> get_ts_input_type(%{type: arg.type, constraints: arg.constraints || []})
    end
  end

  defp non_path_args(route, path_params) do
    path_param_set = MapSet.new(path_params)

    route.arguments
    |> Enum.reject(fn arg -> MapSet.member?(path_param_set, arg.name) end)
  end

  defp build_query_param_and_body([]), do: {nil, []}

  defp build_query_param_and_body(query_args) do
    any_required = Enum.any?(query_args, fn arg -> !arg.allow_nil? && arg.default == nil end)
    optional_marker = if any_required, do: "", else: "?"

    fields =
      Enum.map_join(query_args, "; ", fn arg ->
        base_type = get_ts_input_type(%{type: arg.type, constraints: arg.constraints || []})
        ts_type = if arg.allow_nil?, do: "#{base_type} | null", else: base_type
        opt = if arg.allow_nil? || arg.default != nil, do: "?", else: ""
        "#{format_output_field(arg.name)}#{opt}: #{ts_type}"
      end)

    param = "query#{optional_marker}: { #{fields} }"

    body_lines =
      ["  const searchParams = new URLSearchParams();"] ++
        Enum.map(query_args, fn arg ->
          field = format_output_field(arg.name)
          required = !arg.allow_nil? && arg.default == nil

          # Array arguments append one `name[]=` pair per element — Plug's query
          # parser needs that to rebuild a list. `String(array)` would collapse
          # the whole thing into one comma-joined value.
          set =
            if array_type?(arg.type) do
              "query.#{field}.forEach((v) => searchParams.append(\"#{field}[]\", String(v)));"
            else
              "searchParams.set(\"#{field}\", String(query.#{field}));"
            end

          if required do
            "  #{set}"
          else
            "  if (query?.#{field} !== undefined && query?.#{field} !== null) #{set}"
          end
        end)

    {param, body_lines}
  end

  # Matches both the DSL form (`{:array, :string}`) and the resolved form
  # (`{:array, Ash.Type.String}`) that `Ash.Type.get_type/1` produces.
  defp array_type?({:array, _inner}), do: true
  defp array_type?(_type), do: false

  defp render_action_function(route_info, opts) do
    %{
      route: route,
      path: path,
      method: method,
      path_params: path_params,
      scope_prefix: scope_prefix
    } = route_info

    has_base_path = Keyword.get(opts, :has_base_path, false)
    function_name = build_function_name(route.name, scope_prefix, :action)
    method_upper = method |> to_string() |> String.upcase()

    input_fields = build_input_fields(route, path_params)
    has_input = input_fields != []
    style = AshTypescript.typed_controller_path_params_style()

    input_type_def =
      if has_input do
        type_name = Codegen.route_input_type_name(route, scope_prefix)
        build_input_type_definition(type_name, input_fields) <> "\n"
      else
        ""
      end

    path_param = build_path_param_args(route, path_params, style)

    input_param =
      if has_input do
        type_name = Codegen.route_input_type_name(route, scope_prefix)
        [format_output_field(:input) <> ": " <> type_name]
      else
        []
      end

    config_param = [
      format_output_field(:config) <> "?: TypedControllerConfig"
    ]

    all_params = path_param ++ input_param ++ config_param
    params = Enum.join(all_params, ", ")

    use_path_prefix = style == :object and path_params != []
    url_expr = build_url_template(path, path_params, use_path_prefix, has_base_path)
    jsdoc = build_action_jsdoc(route, method_upper, path)

    body_arg =
      if has_input do
        "JSON.stringify(#{format_output_field(:input)})"
      else
        "undefined"
      end

    config_var = format_output_field(:config)
    action_name_str = format_output_field(route.name)

    {return_type, default_headers_arg} = response_typing(route, scope_prefix)

    input_type_def <>
      """
      #{jsdoc}
      export async function #{function_name}(#{params}): Promise<#{return_type}> {
        return executeTypedControllerRequest(#{url_expr}, "#{method_upper}", "#{action_name_str}", #{body_arg}, #{config_var}#{default_headers_arg});
      }
      """
  end

  # GET routes declaring `returns` are JSON endpoints, so they get a fetch
  # function too. It takes the path helper's parameters and calls the helper
  # for the URL, keeping path/query serialization and the base path in one place.
  defp render_get_fetch_function(route_info) do
    %{route: route, path: path, path_params: path_params, scope_prefix: scope_prefix} =
      route_info

    style = AshTypescript.typed_controller_path_params_style()
    path_param_args = build_path_param_args(route, path_params, style)
    {query_param, _body_lines} = build_query_param_and_body(non_path_args(route, path_params))

    path_helper_args =
      case {style, path_params} do
        {_, []} -> []
        {:object, _} -> [format_output_field(:path)]
        {:args, _} -> Enum.map(path_params, &format_output_field/1)
      end ++ if(query_param, do: ["query"], else: [])

    config_var = format_output_field(:config)

    params =
      Enum.join(
        path_param_args ++ List.wrap(query_param) ++ ["#{config_var}?: TypedControllerConfig"],
        ", "
      )

    function_name = build_function_name(route.name, scope_prefix, :action)
    path_helper_name = build_function_name(route.name, scope_prefix, :path)
    url_expr = "#{path_helper_name}(#{Enum.join(path_helper_args, ", ")})"
    {return_type, default_headers_arg} = response_typing(route, scope_prefix)

    """
    #{build_action_jsdoc(route, "GET", path)}
    export async function #{function_name}(#{params}): Promise<#{return_type}> {
      return executeTypedControllerRequest(#{url_expr}, "GET", "#{format_output_field(route.name)}", undefined, #{config_var}#{default_headers_arg});
    }
    """
  end

  # Fetch function return type and trailing `defaultHeaders` argument. A declared
  # `returns` means the route responds with JSON, so ask for it — as default
  # headers, so `config.headers` can still override it.
  defp response_typing(%{returns: nil}, _scope_prefix), do: {"Response", ""}

  defp response_typing(route, scope_prefix) do
    {"TypedControllerResponse<#{Codegen.route_result_type_name(route, scope_prefix)}>",
     ~s[, { Accept: "application/json" }]}
  end

  @doc """
  Renders the Zod schema for a route's input.

  Returns an empty string for routes without non-path arguments,
  or when Zod schema generation is disabled.
  """
  def render_zod_schema(route_info) do
    if AshTypescript.Rpc.generate_zod_schemas?() do
      schema_name =
        AshTypescript.TypedController.Codegen.route_zod_schema_name(
          route_info.route,
          route_info.scope_prefix
        )

      render_validation_schema(route_info, ZodSchemaGenerator, schema_name)
    else
      ""
    end
  end

  @doc """
  Renders the Valibot schema for a route's input.

  Returns an empty string for routes without non-path arguments,
  or when Valibot schema generation is disabled.
  """
  def render_valibot_schema(route_info) do
    if AshTypescript.Rpc.generate_valibot_schemas?() do
      schema_name =
        AshTypescript.TypedController.Codegen.route_valibot_schema_name(
          route_info.route,
          route_info.scope_prefix
        )

      render_validation_schema(route_info, ValibotSchemaGenerator, schema_name)
    else
      ""
    end
  end

  defp render_validation_schema(route_info, formatter, schema_name) do
    %{route: route, path_params: path_params} = route_info

    input_args = non_path_args(route, path_params)

    if input_args == [] do
      ""
    else
      field_lines =
        Enum.map_join(input_args, "\n", fn arg ->
          # Constraints arrive pre-folded by the FoldArgumentConstraints
          # transformer, so this resolves to the same shape manifest inputs
          # have — and composes through the same shared path.
          spec_type = TypeResolver.resolve(Ash.Type.get_type(arg.type), arg.constraints || [])

          {name, schema_type} =
            SchemaCore.compose_input_field(
              formatter,
              format_output_field(arg.name),
              %{type: spec_type},
              arg.allow_nil?,
              arg.allow_nil? || arg.default != nil
            )

          "  #{name}: #{schema_type},"
        end)

      """
      export const #{schema_name} = #{formatter.object_constructor()}({
      #{field_lines}
      });
      """
    end
  end

  defp build_input_fields(route, path_params) do
    route
    |> non_path_args(path_params)
    |> Enum.map(fn arg ->
      optional = arg.allow_nil? || arg.default != nil
      base_type = get_ts_input_type(%{type: arg.type, constraints: arg.constraints || []})
      ts_type = if arg.allow_nil?, do: "#{base_type} | null", else: base_type
      {format_output_field(arg.name), ts_type, optional}
    end)
  end

  defp build_input_type_definition(type_name, fields) do
    field_defs =
      Enum.map_join(fields, "\n", fn {name, ts_type, optional} ->
        opt = if optional, do: "?", else: ""
        "  #{name}#{opt}: #{ts_type};"
      end)

    "export type #{type_name} = {\n#{field_defs}\n};"
  end

  defp build_function_name(action_name, nil, :path) do
    format_output_field(:"#{action_name}_path")
  end

  defp build_function_name(action_name, scope_prefix, :path) do
    format_output_field(:"#{scope_prefix}_#{action_name}_path")
  end

  defp build_function_name(action_name, nil, :action) do
    format_output_field(action_name)
  end

  defp build_function_name(action_name, scope_prefix, :action) do
    format_output_field(:"#{scope_prefix}_#{action_name}")
  end

  defp build_path_param_args(_route, [], _style), do: []

  defp build_path_param_args(route, path_params, :object) do
    path_fields =
      Enum.map_join(path_params, ", ", fn param ->
        ts_type = get_path_param_type(route, param)
        "#{format_output_field(param)}: #{ts_type}"
      end)

    [format_output_field(:path) <> ": { " <> path_fields <> " }"]
  end

  defp build_path_param_args(route, path_params, :args) do
    Enum.map(path_params, fn param ->
      ts_type = get_path_param_type(route, param)
      "#{format_output_field(param)}: #{ts_type}"
    end)
  end

  defp build_url_template(nil, _path_params, _use_path_prefix, _has_base_path), do: "\"\""

  defp build_url_template(path, [], _use_path_prefix, false), do: "\"#{path}\""

  defp build_url_template(path, [], _use_path_prefix, true),
    do: "`${_basePath}#{path}`"

  defp build_url_template(path, path_params, use_path_prefix, has_base_path) do
    template =
      Enum.reduce(path_params, path, fn param, acc ->
        interpolation = path_param_interpolation(param, use_path_prefix)
        String.replace(acc, ":#{param}", interpolation)
      end)

    prefix = if has_base_path, do: "${_basePath}", else: ""
    "`#{prefix}#{template}`"
  end

  defp build_url_template_to_variable(nil, _path_params, _use_path_prefix, _has_base_path),
    do: "const base = \"\";"

  defp build_url_template_to_variable(path, [], _use_path_prefix, false),
    do: "const base = \"#{path}\";"

  defp build_url_template_to_variable(path, [], _use_path_prefix, true),
    do: "const base = `${_basePath}#{path}`;"

  defp build_url_template_to_variable(path, path_params, use_path_prefix, has_base_path) do
    template =
      Enum.reduce(path_params, path, fn param, acc ->
        interpolation = path_param_interpolation(param, use_path_prefix)
        String.replace(acc, ":#{param}", interpolation)
      end)

    prefix = if has_base_path, do: "${_basePath}", else: ""
    "const base = `#{prefix}#{template}`;"
  end

  # Path-param values are always encodeURIComponent-wrapped so a value can never
  # alter URL structure (e.g. a "/"-prefixed value forming a protocol-relative
  # URL that redirects the request and its credentialed headers to another host).
  defp path_param_interpolation(param, use_path_prefix) do
    var =
      if use_path_prefix do
        "#{format_output_field(:path)}.#{format_output_field(param)}"
      else
        format_output_field(param)
      end

    "${encodeURIComponent(#{var})}"
  end

  defp build_jsdoc(route, path) do
    lines = ["/**"]

    lines =
      if route.description do
        lines ++ [" * #{route.description}"]
      else
        lines ++ [" * Path helper for #{path || ""}"]
      end

    lines = maybe_add_deprecated(lines, route)
    lines = maybe_add_see_tags(lines, route)
    lines = lines ++ [" */"]
    Enum.join(lines, "\n")
  end

  defp build_action_jsdoc(route, method_upper, path) do
    lines = ["/**"]

    lines =
      if route.description do
        lines ++ [" * #{route.description}"]
      else
        lines ++ [" * #{method_upper} #{path || ""}"]
      end

    lines = maybe_add_deprecated(lines, route)
    lines = maybe_add_see_tags(lines, route)
    lines = lines ++ [" */"]
    Enum.join(lines, "\n")
  end

  defp maybe_add_deprecated(lines, route) do
    if route.deprecated do
      deprecation_msg =
        if is_binary(route.deprecated),
          do: route.deprecated,
          else: "This route is deprecated"

      lines ++ [" * @deprecated #{deprecation_msg}"]
    else
      lines
    end
  end

  defp maybe_add_see_tags(lines, route) do
    case Map.get(route, :see, []) do
      [] ->
        lines

      see_list ->
        see_lines =
          Enum.map(see_list, fn route_name ->
            " * @see #{format_output_field(route_name)}"
          end)

        lines ++ see_lines
    end
  end
end
